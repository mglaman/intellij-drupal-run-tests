import org.gradle.api.tasks.testing.logging.TestExceptionFormat

plugins {
    // gradle-intellij-plugin - read more: https://github.com/JetBrains/gradle-intellij-plugin
    id "org.jetbrains.intellij" version "0.4.21"
    // gradle-changelog-plugin - read more: https://github.com/JetBrains/gradle-changelog-plugin
    id "org.jetbrains.changelog" version "0.4.0"
    id 'com.palantir.git-version' version "0.12.3"
}

def htmlFixer = { htmlFile -> file(htmlFile).text.replace('<html>', '').replace('</html>', '') }

apply plugin: 'idea'

apply plugin: 'java'
sourceCompatibility = 1.8
targetCompatibility = 1.8

apply plugin: 'org.jetbrains.intellij'

intellij {
    version ideaVersion
    updateSinceUntilBuild false
    plugins = [
            "com.jetbrains.php:${phpPluginVersion}",
            "com.jetbrains.plugins.ini4idea:${iniPluginVersion}",
            "com.jetbrains.php.drupal:${drupalPluginVersion}",
            'coverage',
            'webDeployment',
            'yaml',
            'CSS',
            'java-i18n',
            'properties',
            'xpath',
            'java'
    ]
    pluginName 'Drupal Test Runner'
}

patchPluginXml {
    sinceBuild '202'
    changeNotes = htmlFixer('src/main/resources/META-INF/change-notes.html')
}

publishPlugin {
    token System.getenv('IJ_TOKEN')
}

group 'com.mglaman.drupal_run_tests'

def details = versionDetails()
if (details.isCleanTag) {
    version = "${details.lastTag}"
} else {
    version = "${details.lastTag}.${details.gitHash}-SNAPSHOT"
}

wrapper {
    gradleVersion '5.5.1'
}

test.testLogging.exceptionFormat = TestExceptionFormat.FULL
